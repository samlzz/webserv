# ============================================================================
# PEG grammar for webserv configuration file
# ============================================================================

# --- Whitespaces and commentary ---
_       <- (SP / EOL / comment)*      # optionnal space/new line/comment
__      <- (SP / comment)+            # at least one space/comment

SP      <- [ \t]+
EOL     <- '\r\n' / '\n' / '\r'
comment <- '#' [^\n\r]* EOL?

# ============================================================================
# Values
# ============================================================================

listen_value      <- host_port / port_only
host_port         <- host:[^:\n\r\t ;{}]+ ':' port:[0-9]+
port_only         <- port:[0-9]+

method_list       <- method (__ method)*
method            <- 'GET' / 'POST' / 'DELETE' / 'PUT' / 'HEAD'

path              <- [^;\n\r\t {}]+


extension         <- ('.' [a-zA-Z0-9]+) (__ ('.' [a-zA-Z0-9]+))*
cookie_value      <- [a-zA-Z] [a-zA-Z0-9_-]*

bool_value        <- 'on' / 'off' / 'true' / 'false' / '1' / '0'

status_codes      <- [2-5][01][0-5] (__ [2-5][01][0-5])*
error_page_value  <- (codes:status_codes __)? path:path

size_value        <- [0-9]+ size_unit?
size_unit         <- 'K' / 'M' / 'G' / 'k' / 'm' / 'g'

redirect_value    <- ([2-5][01][0-5] __)? path

# ============================================================================
# Blocks
# ============================================================================

@config <- _ server+ _

# --- Server Block ---
@server          <- 'server' _ '{' _ server_body _ '}' _

server_body      <- ~(server_directive / location / fatal_block / fatal_directive)+

server_directive <- (listen / methods / root / index / autoindex / error_page 
                 / client_max_body_size / cgi / return_redirect)

# --- Location Block ---
@location          <- 'location' __ path:~path _ '{' _ location_body _ '}' _

location_body      <- ~(location_directive / fatal_directive)+
location_directive <- methods / root / index / autoindex / error_page 
                   / cgi / upload / return_redirect / session_login
				   / set_cookie / vary_cookie

# ============================================================================
# Singles Directives
# ============================================================================

# --- Server Directives ---
listen               <- 'listen' __ ~listen_value _ ~';' _
methods              <- 'methods' __ methods:~method_list _ ~';' _
root                 <- 'root' __ root:~path _ ~';' _
index                <- 'index' __ index:~path _ ~';' _
autoindex            <- 'autoindex' __ autoindex:~bool_value _ ~';' _
@error_page          <- 'error_page' __ ~error_page_value _ ~';' _
client_max_body_size <- 'client_max_body_size' __ client_max_body_size:~size_value _ ~';' _
return_redirect      <- 'return' __ redirect:~redirect_value _ ~';' _
@cgi                 <- 'cgi' __ ext:~extension __ execPath:~path _ ~';' _

# --- Location Directives ---
upload               <- 'upload' __ upload:~path _ ~';' _
session_login        <- 'session_login' __ session_login:~bool_value _ ~';' _
@set_cookie          <- 'set_cookie' __ name:~cookie_value _ ~';' _
@vary_cookie         <- 'vary_cookie' __ name:~cookie_value _ ~';' _

# --- Unknow directives (throw error) ---
fatal_block          <- !~!( [^;{}\t]+ _ '{' _ unknown_directive* _ '}' _ )
fatal_directive      <- !~!( unknown_directive )
unknown_directive    <- [^;{}]+ ~';' _
